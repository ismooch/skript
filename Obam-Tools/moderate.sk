command /g <text> [<number>] [<number>] [<number>]:
	description: Teleports to a Player
	permission: obam.mod
	trigger:
		{monitor.%player%} is not set:
			set {monitor.%player%} to false
		player does not have permission "obam.admin":
			{monitor.%player%} is false:
				send "&cYou must be in monitor mode to do this!" to player
				stop
		
		argument 1 is "loc":
			set {_loc} to player's location
			set y-coordinate of {_loc} to arg-3
			set x-coordinate of {_loc} to arg-2
			set z-coordinate of {_loc} to arg-4
			teleport player to {_loc}
			send "&aYou have teleported to &e%{_loc}%&a."
			stop
		set {_player} to argument 1 parsed as player
		teleport player to {_player}
		send "&aYou have teleported to %{_player}%." to player
		log "%player%^GO ACTION^%{_player}%^%location of {_player}%" to "admin/actions.log"
		
		
command /grab <player>:
	permission: obam.mod
	trigger:
		player-argument is player:
			send "&cYou should not grab yourself in public!" to player
			stop
		set {_loc} to location of player-argument
		teleport player-argument to player
		
		send "&aYou have snatched %player-argument%." to player
		send "&aYou have been snatched by %player%!" to player-argument
		log "%player%^%location of player%^GRAB ACTION^%player-argument%^%{_loc}%" to "admin/actions.log"
		
command /monitor:
	permission: obam.mod
	trigger:
		
		{monitor.%player%} is true:
			set {monitor.%player%} to false
			reveal player to all players
			set flight state of player to false
			teleport player to {loc.mon.%player%}
			set {tim.mon.%player%} to difference between {tim.mon.%player%} and now
			send "&aYou are no longer in monitor mode! You have been returned to your position!"
			log "%player%^MONITOR DISABLE ACTION^%{loc.mon.%player%}%^%{tim.mon.%player%}%" to "admin/actions.log"
			stop
		{monitor.%player%} is not set:
			set {monitor.%player%} to false
		{monitor.%player%} is false:
			set {loc.mon.%player%} to location of player
			set {monitor.%player%} to true
			set {tim.mon.%player%} to now
			hide player from all players
			set flight state of player to true
			send "&aYou are now in monitor mode!" to player
			log "%player%^MONITOR ENABLE ACTION^%{loc.mon.%player%}%" to "admin/actions.log"
			
			
every 1 second:
	loop all players:
		{monitor.%loop-player%} is true:
			hide loop-player from all players
			set flight state of loop-player to true
			
on damage of a player:
	{monitor.%victim%} is true:
		cancel event
		
on target:
	target is a player:
		set {_player} to target
		{monitor.%{_player}%} is true:
			cancel event
		
on pick up:
	{monitor.%player%} is true:
		cancel event
		
on right click:
	{monitor.%player%} is true:
		cancel event

on left click:
	{monitor.%player%} is true:
		cancel event
