options:
	crudepack: ink sack named "&3Crude Pack" with lore "&6Use /backpack to||&6finish crafting this item"
	backpack: ink sack named "&3BackPack"
	enderpack: ender pearl named "&3EnderPack" with lore "&6Open your Ender Chest||&6anywhere!|| ||&5&oRight Click to open||&5&oYour portable Ender Chest"

on script load:
	register new shaped recipe for {@crudepack} using emerald block, emerald block, emerald block, emerald block, ink sack, emerald block, emerald block, chest, emerald block
	register new shaped recipe for {@enderpack} using air, air, air, emerald block, ender pearl, emerald block, air, ender chest, air
	{count.backpacks} is not set:
		set {count.backpacks} to 1

on craft of ink sack:
	name of event-item is "&3Crude Pack"
	send "&aPlease use &e/backpack&a, to activate this backpack!"

command /backpack [<text>] [<text>] [<text>]:
	aliases: bp
	trigger:
		argument 1 is "help" or "h":
			message "&3--[&6&lBackpack Help&3]--"
			message "&cRequired = [] Optional = ()"
			message "&a * /backpack"
			message "&7 Use while holding a Crude Pack to finish crafting a Backpack"
			message "&a * /backpack [name] [namethatyouwant]"
			message "&7 Use while holding a Backpack to rename it."
			message "&a * /backpack [start]"
			message "&7 Used by Admins to start chest placement in a new world."
			message "&a * /backpack [missing] [number]"
			message "&7 Used by Admins to create a backpack with a specific number."
			message "&a * /backpack [fix] [number]"
			message "&7 Used by Admins to fix a backpack if a chest glitches out."
			message "&a * /backpack [give] [number]"
			message "&7 Used by Admins to get Crude Packs if needed."
			message "&a * /backpack [warning] [number]"
			message "&7 Used by Admins to view warnings, no number for all."
			message "&a * /backpack [warning] [delete] [number]"
			message "&7 Used by Admins to remove specific warnings."
			send " "
			send " "
			send "&6Developed for &7OBAMCraft &eBy: &aThe OBAM Mechanics"
			send "&cV 1.0"
		argument 1 is not set:
			player has permission "obam.free":
				player's tool is ink sack:
					{backpack.spacestop} is true:
						broadcast "&cWARNING: The magical land of Backpacks is bursting at the seams. Please notify the powers that be!"
						stop
					name of player's tool is "&3Backpack":
						set {_lorecheck} to uncoloured lore of player's tool
						set {_lorecheck::*} to {_lorecheck} split at "||"
						{_lorecheck::2} is not set:
							send "&cYou need to trade this item into a mod to receive a Crude Pack" to player
							stop
						{_lorecheck::2} is set:
							send "&cThis backpack is already assigned" to player
							stop
					name of player's tool is "&3Crude Pack":
						set {_lorecheck} to uncoloured lore of player's tool
						set {_lorecheck::*} to {_lorecheck} split at "||"
						{_lorecheck::2} is set:
							remove {@crudepack} from player
							player doesn't have space for a oak sapling:
								message "&cWarning: You do not have space for this backpack. Please make room in your inventory."
								give player {@crudepack}
								stop
							player has space for a oak sapling:
								give player {@backpack} with lore "&8%{count.backpacks}%||&6Carry lots of extra stuff!|| ||&5&oRight click to open"
							set {_loc} to {backpack.coordinate}
							set {_chest.type} to {backpack.chest.type}
							set block at location of {_loc} to {_chest.type}
							{backpack.sign.count} is 1:
								set {_sign} to the location 1 meter south of {_loc}
								set block at location of {_sign} to wall sign:3
								set {_line} to block at location of {_sign}
								set line 2 of {_line} to "%{count.backpacks}%"
							add 1 to {backpack.sign.count}
							{backpack.sign.count} is 21:
								set {backpack.sign.count} to 1
							set {_chest} to block at {_loc}
							set {loc.backpack.%{count.backpacks}%} to {_chest}
							{loc.backpack.%{count.backpacks}%} is set:
								send "&aBackpack ##%{count.backpacks}% created..."
							{loc.backpack.%{count.backpacks}%} is not set:
								send "&cError..."
							add 1 to y-coordinate of {backpack.coordinate}
							set {_height.check} to y-coordinate of {backpack.coordinate}
							{_height.check} is 256:
								set y-coordinate of {backpack.coordinate} to 1
								add 1 to x-coordinate of {backpack.coordinate}
								set {backpack.sign.count} to 1
								set {_chest.type} to {backpack.chest.type}
								{_chest.type} is chest:
									set {backpack.chest.type} to trapped chest
								{_chest.type} is trapped chest:
									set {backpack.chest.type} to chest
							set {_length.check} to x-coordinate of {backpack.coordinate}
							{_length.check} is greater than 127:
								set x-coordinate of {backpack.coordinate} to -127
								add 2 to z-coordinate of {backpack.coordinate}
							set {_width.check} to z-coordinate of {backpack.coordinate}
							{_width.check} is greater than 127:
								set {backpack.spacestop} to true
							add 1 to {count.backpacks}
							stop
				message "&cYou must have a &a&b&k&a&3Crude Pack &cin your hand to use this command!"
		argument 1 is "name" or "n":
			player has permission "obam.free":
				argument 2 is not set:
					message "&cPlease type a name in argument 2 to rename this bag."
				argument 2 is set:
					name of player's tool is "&3Crude Pack":
						send "&cYou need to finish crafting this with /backpack first"
						stop
					name of player's tool is "&3BackPack":
						set {_lorecheck} to uncoloured lore of player's tool
						set {_lorecheck::*} to {_lorecheck} split at "||"
						{_lorecheck::2} is not set:
							send "&cYou need to trade this item into a mod to receive a Crude Pack" to player
							stop
						lore of player's tool contains "&eName:":
							send "&cThis backpack already has a name!"
							stop
						the length of argument 2 is greater than 30:
							send "&cThat name is too long!"
							stop
						set lore of player's tool to "%lore of player's tool%|| ||&eName:||&2%argument 2%"
						send "&aName of backpack set to &2%argument 2%"
						stop
					message "&cYou must have a &a&b&k&a&3Backpack &cin your hand to use this command!"
		argument 1 is "start" or "s":
			player has permission "obam.admin":
				world is "bagworld":
					set {backpack.coordinate} to player's location
					set x-coordinate of {backpack.coordinate} to -128
					set y-coordinate of {backpack.coordinate} to 1
					set z-coordinate of {backpack.coordinate} to -128
					message "Backpack Start set to %{backpack.coordinate}%"
					set {backpack.sign.count} to 1
					set {backpack.chest.type} to chest
					set {count.backpacks} to 1
					set {backpack.spacestop} to false
		argument 1 is "missing" or "m":
			player has permission "obam.smod":
				argument 2 is not set:
					message "&cPlease type a number in argument 2 to get that number of backpack."
				argument 2 is set:
					give player {@backpack} with lore "&8%argument 2%||&6Carry lots of extra stuff!|| ||&5&oRight click to open"
		argument 1 is "fix" or "f":
			player has permission "obam.admin":
				argument 2 is not set:
					message "&cPlease type a number in argument 2 to get that number of backpack."
				argument 2 is set:
					give player {@backpack} with lore "&8%argument 2%||&6Carry lots of extra stuff!|| ||&5&oRight click to open"
					set {_loc} to {tie.backpack.%argument 2%}
					set {_type.check} to the location 1 meter west of {_loc}
					block at location of {_type.check} is trapped chest:
						set block at location of {tie.backpack.%argument 2%} to chest
						stop
					set block at location of {tie.backpack.%argument 2%} to trapped chest
		argument 1 is "give" or "g":
			player has permission "obam.smod":
				set {_qty} to argument 3 parsed as an integer
				argument 2 is "crudepack":
					player doesn't have space for {_qty} of {@crudepack}:
						message "&cYou do not have room for this."
						stop
					give {_qty} of {@crudepack} to player
					message "&aHere are your things."
					stop
				argument 2 is "enderpack":
					player doesn't have space for {_qty} of {@enderpack}:
						message "&cYou do not have room for this."
						stop
					give {_qty} of {@enderpack} to player
					message "&aHere are your things."
					stop
		argument 1 is "warnings" or "w":
			player has permission "obam.admin":
				argument 2 is not set:
					message "&aCurrent Backpack Warnings"
					loop {warnings.backpacks::*}:
						set {_num} to loop-index
						set {_warn} to loop-value
						message "&a%{_num}%. %{_warn}%"
					stop
				argument 2 is "delete" or "d":
					argument 3 is not set:
						message "&cPick a number."
						stop
					set {_num} to argument 3
					{warnings.backpacks::%{_num}%} does not exist:
						message "&cWarning &f%{_num}% &cdoes not exist."
						stop
					delete {warnings.backpacks::%{_num}%}
					message "&aDeleted warning &f%{_num}%"
					stop
				set {_num} to argument 3
				{warnings.backpacks::%{_num}%} does not exist:
					message "&cWarning &f%{_num}% &cdoes not exist."
					stop
				message "&aSelected Backpack Warning"
				message "&a%{_num}%. %{warnings.backpacks::%{_num}%}%"
				stop
on join:
	player has permission "obam.admin":
		{warnings.backpacks::*} is set:
			wait 4 seconds
			message "&3There are Backpack warnings. Please contact &2ob1bob69"

on right click with ink sack:
	name of player's tool is "&3Backpack" or "&a&b&k&a&3Backpack":
		set {_chest} to uncoloured lore of player's tool
		set {_chest::*} to {_chest} split at "||"
		{_chest::2} is not set:
			send "&cYou need to trade this item into a mod to receive a Crude Pack" to player
			stop
		{loc.backpack.%{_chest::1}%} is not set:
			send "&cChest is missing"
			stop
		{loc.backpack.%{_chest::1}%} is set:
			block at location of {loc.backpack.%{_chest::1}%} is not chest or trapped chest:
				send "&cChest is missing, contact &2ob1bob69 &cfor assistance"
				set {_num} to size of {warnings.backpacks::*}
				add 1 to {_num}
				set {warnings.backpacks::%{_num}%} to "Chest %{_chest::1}% by %player% on %now%"
			open {loc.backpack.%{_chest::1}%} to player

on right click with ender pearl:
	name of player's tool contains "&3EnderPack":
		cancel event
		open player's enderchest to player

on shoot:
	shooter's tool is ender pearl
	name of shooter's tool contains "&3EnderPack":
		cancel event

#Diagnostics
command /backpackcord:
	permission: obam.admin
	trigger:
		message "%{backpack.coordinate}%"

command /backpackcount:
	permission: obam.admin
	trigger:
		message "%{count.backpacks}%"

command /backpackloc [<text>]:
	permission: obam.admin
	trigger:
		argument 1 is not set:
			message "%{loc.backpack.%{count.backpacks}%}%"
			stop
		set {_num} to argument 1
		message "%{loc.backpack.%{_num}%}%"
		set {_loc} to location of {loc.backpack.%{_num}%}
		message "%{_loc} %"